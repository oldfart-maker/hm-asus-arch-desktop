pkgname=rtw89-morrownr-dkms-git
pkgver=1.0.1554.e47a21c
pkgrel=1
pkgdesc="Realtek rtw89 Wi-Fi 6/6E drivers (morrownr fork) with DKMS support (USB + PCIe)"
arch=('x86_64')
url="https://github.com/morrownr/rtw89"
license=('GPL2')
depends=('dkms')
makedepends=('git')
source=("git+${url}.git"
        "dkms.conf")
md5sums=('SKIP'
         'SKIP')

# Use git describe to generate a nice local version string
pkgver() {
  cd "${srcdir}/rtw89"
  # e.g. r1554.e47a21c
  printf "1.0.%s" "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  local dkms_name="rtw89-morrownr"
  cd "${srcdir}/rtw89"

  install -d "${pkgdir}/usr/src/${dkms_name}-git"

  # copy driver sources
  cp -r . "${pkgdir}/usr/src/${dkms_name}-git"

  # overwrite dkms.conf with the one we ship (in srcdir, not the repo)
  install -m644 "${srcdir}/dkms.conf" "${pkgdir}/usr/src/${dkms_name}-git/dkms.conf"
}
